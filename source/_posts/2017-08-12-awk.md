---
title: awk 使用
tags:
  - awk
categories:
  - 命令行工具
date: 2017-08-12 16:00:17
---


## 背景知识

- 上世纪 70 年代在贝尔实验室开发出来
- 名字是由几个作者的姓组合而来的 Alfred Aho， Peter Weinberger， 以及 Brian Kernaghan
- Sed is used to process and modify text
- Awk is mostly used as a tool for **analysis** and **reporting**
- 有迭代 iteration 和 变量 variables，对比`Sed` 更像一门编程语言
- 结合 sed 和 awk 一起使用会有 **incredibly power**
- [Guide](http://www.delorie.com/gnu/docs/gawk/gawk.html#SEC_Top)

## 工作方式

Awk works by reading a text file or input stream one line at a time. Each line is scanned to see if it matches a predefined pattern. If a match is found, an action is performed.

## 语法

- Awk 语句只能被**单引号包含**
- `$1`..`$n` 表示第n列，`$0`表示整行，默认以**空格**或**tab**分隔

```bash
awk '{print $1, $4}' file
```

### 内建变量

|内建变量|含义|
|--|--|
|`FS`|输入字段分隔符，默认为空格或tab|
|`OFS`|输出字段分隔符|
|`RS`|输入行分隔符，默认为`\n`|
|`ORS`|输出行分隔符|
|`NF`|当前记录有多少列（字段数）|
|`NR`|行号，从1开始，多个文件会累加|
|`FNR`|各文件自己的行号|
|`FILENAME`|当前输入文件的名字|

### 格式化输出，类似C语言的`printf`

```bash
awk '{printf "%-8s %-8s %-15s\n", $1, $2, $3}' file
```

### 条件过滤

```bash
awk '$3==0 && $6=="LISTEN"' file
awk '$3>0 {print $0}' file
awk '$3==0 && $6=="LISTEN" || NR==1' file # 保留第一行
```

### 指定分隔符

- BEGIN 块中assign **FS** 变量
- 使用 `-F` option

```bash
awk 'BEGIN{FS=":"} {print $1, $3, $6}' file
awk -F: '{print $1, $3, $6}' file
awk -F '[;:]' '{print $1, $3, $6}' file # 指定多个分隔符
awk -F: '{print $1, $3, $6}' OFS="\t" file # 指定输出分隔符
```

### 字符串匹配 **~**

```bash
awk '$6 ~ /pattern/ || NR==1 {print NR, $4, $5}' OFS="\t" file
awk '/pattern/' file
awk '!/pattern/' file # 模式取反
awk '$6 ~ /pattern1|pattern2/ {print $4, $5}' file
```

### 拆分文件

根据某一列使用重定向符号

```bash
awk 'NR!=1 {print > $6}' file
awk 'NR!=1 {print $4, $5 > $6}' file
```

### 使用环境变量

将外部变量值传递给 awk，借助 `-v` 选项，或放在语句块后面

`-v`和`ENVIRON` （ENVIRON的变量需要export）

```bash
awk -v val=$x '{print $1, ENVIRON["y"]}' file
```

放在 `BEGIN` `{}` `END` 语句块后面**不需要**使用 `-v` 选项

```bash
awk '{print v1, v2}' v1=$var1 v2=$var2 filename
```

### 从 awk 中读取命令输出

通过使用 `getline` 将外部 shell 命令的输出读入变量 cmdout

```bash
"cmd" | getline cmdout;
echo | awk '{ "grep root /etc/passwd" | getline cmdout; print cmdout }'
```

### 循环

```awk
for(i=0;i<10;i++)
  print $i
for(i in array)
  print array[i]
```

### Awk 脚本

假设有以下统计脚本 **wordCount.awk**

```awk
#!/bin/awk -f
{
  for (i = 1; i <= NF; i++)
    freq[$i]++
}

END {
  for (word in freq)
    printf "%s\t%d\n", word, freq[word]
}
```

#### 执行脚本

使用 `-f` 选项

```bash
awk -f wordCount.awk DATA_FILE
```

或

```bash
./wordCount.awk DATA_FILE
```

## 示例

```bash
# 批量删除 git 分支
git branch -r | awk -F/ '/origin\/feature\/*/ {print $2,$3}' OFS='\/' | xargs -I {} git push origin :{}

# 查找并删除重复文件脚本

#!/bin/bash
ls -lS --time-style=long-iso | awk 'BEGIN {
  getline; getline; # 忽略第一行的统计信息，从第二行开始
  name1=$8; size=$5 # 第8列是文件名，第5列是文件大小
}
{
  # 下一个文件从第三行开始
  name2=$8;
  if (size==$5)
  {
    "md5sum "name1 | getline; csum1=$1; # 读取外部命令的输出
    "md5sum "name2 | getline; csum2=$1;
    if ( csum1==csum2 )
    {
      print name1; print name2 # 大小相同，且校验和相同则认为两个文件重复，awk打印两行
    }
  };
  size=$5; name1=name2;
}' | sort -u > duplicate_files
# 所有重复的行排序后取唯一保存

# 把一堆有关联的文件行依次求校验和，排序，按32个字符的md5值取唯一再拿到唯一后的文件名就是不重复的文件。
cat duplicate_files | xargs -I {} md5sum {} | sort | uniq -w 32 | awk '{ print $2 }' | sort -u > duplicate_sample

echo Removing...
comm duplicate_files duplicate_sample -2 -3 | tee /dev/stderr | xargs rm
echo Removed duplicates files successfully.
```
